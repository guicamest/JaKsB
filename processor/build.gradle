plugins {
    alias(libs.plugins.kotlin.jvm)
    id 'java-library'
    id 'jvm-test-suite'
    id 'jacoco'
    alias(libs.plugins.ktlint.gradle)
//    id "app.cash.licensee" version "1.12.0"
}

kotlin {
    jvmToolchain(project.getProperties().getOrDefault("kotlin.jvmToolchain", 11) as Integer)
}

dependencies {
    implementation libs.kotlin.poet
    implementation libs.kotlin.poet.ksp
    implementation libs.kotlin.symbol.processing
    implementation libs.jakarta.xml

    testImplementation libs.kotlin.compiler.tester
}

ktlint {
    version = libs.versions.ktlint
}

jacoco {
    toolVersion = "0.8.12"
}
testing {
    suites {
        withType(JvmTestSuite).configureEach {
            useJUnitJupiter('5.10.0')
            dependencies {
                implementation 'org.assertj:assertj-core:3.26.3'
            }
        }
    }
}

tasks.withType(Test) {
    jacoco.includes = ['io.github.guicamest.jaksb.*']
}

testlogger {
    slowThreshold 1000
}

jacocoTestReport {
    dependsOn test
}

java {
    withSourcesJar()
}

tasks.withType(Jar) {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
        )
    }
}

//licensee {
//    allow('Apache-2.0')
//}